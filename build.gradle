plugins {
    id "nu.studer.plugindev" version "1.0.3"
    id "com.ullink.functions" version "1.0"
    id "com.jfrog.bintray" version "1.2"
    id 'net.researchgate.release' version '2.3.4'
}

apply plugin: 'groovy'

group = 'com.ullink.gradle'
description 'gradle-ikvm-plugin is a Gradle plugin for IKVM artifact compilation'

sourceCompatibility = 1.6

dependencies {
    compile fileTree(dir: "${gradle.gradleHomeDir}/lib/plugins", include: "gradle-plugins-*.jar")
    compile getTools()
    compile 'org.apache.commons:commons-lang3:3.4'
    compile 'commons-io:commons-io:2.5'
    compile 'commons-codec:commons-codec:1.10'
    testCompile 'junit:junit:4.8.2'
}

bintray {
    // to remove when upgraded plugindev to 1.0.4
    user project.properties.bintrayUser
    key project.properties.bintrayApiKey
    pkg.repo = 'gradle-plugins'
    pkg.version.gpg.sign = true
    pkg.userOrg = 'ullink'
}

plugindev {
    pluginId 'com.ullink.ikvm'
    pluginName 'com.ullink.gradle:gradle-ikvm-plugin'
    pluginImplementationClass 'com.ullink.IkvmPlugin'
    pluginDescription project.description
    pluginLicenses 'Apache-2.0'
    pluginTags 'gradle', 'ikvm', 'java'
    authorId 'gluck'
    authorName 'Francois Valdy'
    authorEmail 'francois.valdy@gmail.com'
    projectUrl "https://github.com/Ullink/${project.name}"
    projectInceptionYear '2012'
    done()
}

if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}

project.tasks.afterReleaseBuild.dependsOn project.tasks.bintrayUpload
